plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
}
configurations {
    includeImplementation.extendsFrom implementation
}

dependencies {
    // Fabric
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Meteor
    modImplementation "meteordevelopment:meteor-client:${project.meteor_version}"

    // Web scraping
    implementation 'org.jsoup:jsoup:1.16.1'
    includeImplementation 'org.jsoup:jsoup:1.16.1'
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version

        filter { line -> line.replace("@mc_version@", project.minecraft_version) }
        filter { line -> line.replace("@gh_hash@", System.getenv("GITHUB_SHA") ?: "") }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding("UTF-8")
}

jar {
    from {
        configurations.includeImplementation
        .collect { it.isDirectory() ? it : zipTree(it) }
    }
}
